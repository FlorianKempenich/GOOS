plugins {
    id 'java'
    id 'application'
}

repositories {
    jcenter()
}


ext {
    smackVersion = '4.3.0'
}

dependencies {
    /* Code */
    implementation 'com.google.guava:guava:28.1-jre'

    // Smack - XMPP Java Client
    implementation "org.igniterealtime.smack:smack-java7:${smackVersion}"
    implementation "org.igniterealtime.smack:smack-tcp:${smackVersion}"
    implementation "org.igniterealtime.smack:smack-im:${smackVersion}"
    implementation "org.igniterealtime.smack:smack-extensions:${smackVersion}"


    /* Tests */
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.5.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation "org.mockito:mockito-core:2.+"
    testImplementation 'org.mockito:mockito-junit-jupiter:3.+'

    // UI Testing framework for Swing
    testImplementation 'com.googlecode.windowlicker:windowlicker-swing:r268'
}

application {
    mainClassName = 'auctionsniper.Main'
}

test {
    useJUnitPlatform {
        excludeTags 'E2E', 'Integration'
    }
}

task testIntegration(type: Test) {
    description = 'Runs Integration Tests'
    useJUnitPlatform {
        includeTags 'Integration'
        excludeTags 'sandbox'
    }
    shouldRunAfter test
}

task testE2E(type: Test) {
    description = 'Runs End-to-End Tests'
    useJUnitPlatform {
        includeTags 'E2E'
        excludeTags 'sandbox'
    }
    shouldRunAfter test
}
